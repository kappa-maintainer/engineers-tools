// @file build.gradle
// Engineer's tools mod gradle build relay (mc1.12.2)
//
buildscript {
  repositories {
    jcenter()
    maven { url = "http://files.minecraftforge.net/maven" }
  }
  dependencies {
    classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
  }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'idea'
sourceCompatibility = targetCompatibility = '1.8'
compileJava { // Need this here so eclipse task generates correctly.
  sourceCompatibility = targetCompatibility = '1.8'
}

//-----------------------------------------------------------------------------
version = "${version_engineerstools}"
group = "wile.engineerstools"
archivesBaseName = "engineerstools-${version_minecraft}"

def signing = { ->
  def sp = new Properties()
  if(file("signing.properties").exists()) file("signing.properties").withInputStream { sp.load(it) }
  return sp
}()

def git_version = { ->
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'log', '-1', '--format=%h'
    standardOutput = stdout
  }
  return stdout.toString().trim()
}()

minecraft {
  version = "${version_minecraft}-${version_forge}"
  runDir = "run"
  mappings = "stable_39"
  replace '@MOD_BUILDID@', "#${git_version}"
  replace '@MOD_SIGNSHA1@', signing.getProperty('fingerprint_sha1')
  replace '@MOD_VERSION@', "${version_engineerstools}"
  replace '@MOD_MCVERSION@', "${version_minecraft}"
  replaceIn "ModEngineersTools.java"
}

repositories {
  maven { name = "Progwml6 maven"; url = "https://dvs1.progwml6.com/files/maven/" } // JEI files
  maven { name = "ModMaven"; url = "modmaven.k-4u.nl" } // JEI files, fallback
  maven { name = "CurseForge"; url = "http://minecraft.curseforge.com/api/maven/" }
  maven { name = "thiakil"; url = "http://maven.thiakil.com" }
}
dependencies {
  deobfProvided "mezz.jei:jei_${version_minecraft}:${version_jei}:api"
  runtime "mezz.jei:jei_${version_minecraft}:${version_jei}"
  deobfCompile "com.azanor.baubles:Baubles:${version_baubles}"
}

processResources {
  inputs.property "version", project.version
  inputs.property "mcversion", project.minecraft.version
  from(sourceSets.main.resources.srcDirs) {
    include 'mcmod.info'
    expand 'version':project.version, 'mcversion':project.minecraft.version
  }
  from(sourceSets.main.resources.srcDirs) {
    exclude 'mcmod.info'
  }
}

task signJar(type: SignJar, dependsOn: reobfJar) {
  onlyIf { signing.getProperty("keystore_file") }
  keyStore = signing.getProperty("keystore_file")
  alias = signing.getProperty("keystore_alias")
  storePass = signing.getProperty("keystore_pass")
  keyPass = signing.getProperty("keystore_keypass")
  inputFile = jar.archivePath
  outputFile = jar.archivePath
}
build.dependsOn signJar

// EOF
